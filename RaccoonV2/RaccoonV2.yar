rule RaccoonV2 : loader stealer
{
    meta:
        author      = "@_FirehaK <yara@firehak.com>"
        date        = "2022-06-04"
        description = "This rule detects Raccoon Stealer version 2.0 (called Recordbreaker before attribution). It has been spotted spreading through fake software cracks and keygens as far back as April 2022."
        modified    = "2022-08-10"
        reference   = "https://www.zerofox.com/blog/brief-raccoon-stealer-version-2-0/"
        tlp         = "WHITE"

    strings:
        $external_imports = { ff35???????? a1???????? 56 ffd0 ff35???????? a3 }
        $winapi_imports_01 = { 57 68???????? 50 ffd6 68???????? a3 }
        $winapi_imports_02 = { ffd0 8b0d???????? 8bd8 68???????? ffd1 8b0d???????? 68???????? 8945?? ffd1 }
        $winapi_imports_03 = { ff15???????? 68???????? ff75?? ffd6 8b75?? 68???????? 56 a3???????? ffd0 }
        $dexor_c2 = { 68???????? ff?? 8bc8 33d2 8b45fc f7f1 8a0e 8b45fc 328a???????? 40 880c33 46 8945fc 83f840 72 }
        $process_c2 = { 50 6a40 ffd6 8b0d???????? 8bf0 68???????? 57 33db ffd1 85c0 74?? 8a07 3c20 74?? 8bce 2bcf 880439 43 47 8a07 3c20 75 }
        $to_wide_char = { a1???????? 53 56 57 8bf9 57 ffd0 8b35???????? 8bd8 8d145d10000000 52 6a40 ffd6 53 8bf0 56 6aff 57 6a00 68e9fd0000 ff15 }
        $unknown = { 85c9 74?? 0fb73c30 6685ff 74?? 66893e 83c602 49 83ea01 75?? 5f 33c9 b87a000780 }

    condition:
        uint16(0) == 0x5a4d
        and $external_imports
        and all of ($winapi_imports_*)
        and (
            $dexor_c2
            or $process_c2
        )
        and $to_wide_char
        and $unknown
}
